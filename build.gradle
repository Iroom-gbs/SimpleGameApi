plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'maven-publish'
    id 'signing'
}

group = 'me.ddayo'
archivesBaseName = 'simple-game-api'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "me.ddayo:coroutine:1.0.1"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = archivesBaseName
            version = version
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = "Simple game api"
                description = 'Simple mini game api for minecraft spigot plugin (1.16+)'
                url = 'https://github.com/iroom-gbs/simplegameapi'

                scm {
                    url = 'https://github.com/Iroom-gbs/SimpleGameApi.git'
                    connection = 'https://github.com/Iroom-gbs/SimpleGameApi.git'
                    developerConnection = 'https://github.com/Iroom-gbs/SimpleGameApi.git'
                }

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'dayo05'
                        name = 'Jimin Kim'
                        email = 'dayo05@daum.net'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = project.username
                password = project.password
            }
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
        }
    }
}

signing {
    sign publishing.publications.maven
}